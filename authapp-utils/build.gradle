buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.1.RELEASE")
    }
}

allprojects {
    group 'com.norulesweb.authapp.utils'

    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:all"
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.1.RELEASE'
        }
    }
    configurations {
        // For .war repackaging
        providedRuntime
    }

    dependencies {
        compile("org.apache.commons:commons-lang3")
        compile("org.hibernate:hibernate-validator") // Bean validator is required by JPA if not running in a servlet container that provides this
        compile("org.glassfish:javax.el:3.0.0")      // javax.el implementation is required by JPA if not running in a servlet container that provides this
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        compile("mysql:mysql-connector-java")

        compile project(":authapp-core")

        testCompile group: 'junit', name: 'junit', version: '4.11'
    }
}

project(":authapp-utils:core") {
    jar {
        baseName = "authapp-utils"
    }

    dependencies {
        compile("org.apache.commons:commons-collections4:4.1")
    }
}
configure( [
        project(":authapp-utils:authapp-data-load")
] ) {
    apply plugin: 'spring-boot'

    dependencies {
        compile("commons-cli:commons-cli:1.3.1")
        compile project(":authapp-utils:core")
    }
}


project(":authapp-utils:authapp-data-load") {
    jar {
        baseName = "authapp-utils-data-load"
    }
}
